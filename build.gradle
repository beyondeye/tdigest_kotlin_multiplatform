buildscript {
    ext.io_version ='0.1.7'      //https://github.com/Kotlin/kotlinx-io
    ext.tsgenerator_version= '2.9.456' // https://github.com/vojtechhabarta/typescript-generator
    ext.atomicfu_version = '0.12.3' //https://github.com/Kotlin/kotlinx.atomicfu
    ext.serialization_version= '0.11.0' //https://github.com/Kotlin/kotlinx.serialization
    ext.kotlin_version = "1.3.30"
    dependencies {
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:$atomicfu_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.30'
}
repositories {
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}
group 'com.plensee'
version '0.0.1'



apply plugin: 'maven-publish'
apply plugin: 'kotlinx-atomicfu'
apply plugin: 'kotlinx-serialization'

kotlin {
    jvm()
    js()
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    mingwX64("mingw")
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-io:$io_version"
                implementation "org.jetbrains.kotlinx:atomicfu-common:$atomicfu_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$io_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                compileOnly "org.jetbrains.kotlinx:atomicfu:$atomicfu_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation 'com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.1.11'
                implementation 'com.google.guava:guava:19.0'
                implementation 'org.apache.mahout:mahout-math:0.9'
                implementation 'org.apache.commons:commons-lang3:3.4'
                implementation 'com.clearspring.analytics:stream:2.5.2'
                runtimeOnly "org.jetbrains.kotlinx:atomicfu:$atomicfu_version"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-io-js:$io_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
                compileOnly "org.jetbrains.kotlinx:atomicfu-js:$atomicfu_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
                runtimeOnly "org.jetbrains.kotlinx:atomicfu-js:$atomicfu_version"
            }
        }
        mingwMain {
        }
        mingwTest {
        }
    }
}