/*
 * Copyright 2017-2018 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

def prop(name, defVal) {
    def value = project.properties[name]
    if (value == null) return defVal
    return value
}

static distTag(version) {
    def i = version.indexOf('-')
    if (i > 0) return version.substring(i + 1)
    return "latest"
}

//template dir with package.json and readme.md
def npmTemplateDir = file("$projectDir/npmtemplate")
def npmDeployDir = file("$buildDir/npmtemplate")
def typescriptTestsTDigestLibDeployDir = file("$projectDir/typescript_tests/libs/tdigest-kt")
def npmDeployTag = distTag(version)

//def authToken = prop("npmjs.auth.token", "")
def authToken = project.hasProperty('NPM_TOKEN') ? project.property('NPM_TOKEN') : System.getenv('NPM_TOKEN')
def dryRun = prop("dryRun", "false")


// Note: publish transformed files using dependency on sourceSets.main.output
afterEvaluate {
    task preparePublishNpm(type: Copy,dependsOn:[compileKotlinJs]) {
        from(npmTemplateDir) {
            exclude "**/*.d.ts" //do not expand type definition file: see https://stackoverflow.com/questions/25152045/gradle-copy-files-and-expand-only-some-of-them-and-or-ignore-dollar-signs-in-oth
            expand(project.properties + [kotlinDependency: "\"kotlin\": \"$kotlin_version\""])
        }
        //now copy .d.ts files without gradle properties expansion
        from(npmTemplateDir) {
            include "**/*.d.ts"
        }
        from(compileKotlinJs.destinationDir)
        into npmDeployDir
    }
    //copy compiled tdigest library to typescript tests (but without typescript type definition, since the
    //purpose of typescript tests is exactly working on these typescript definitions
      task prepareTypescriptTests(type: Copy,dependsOn: [preparePublishNpm]) {
        from(npmDeployDir) {
            exclude 'package.json'
            exclude 'tdigest-kt.d.ts'
        }
        into typescriptTestsTDigestLibDeployDir
    }
    //make sure the tests are executed  after atomicfu transformed all files
    preparePublishNpm.mustRunAfter{
        transformJsMainAtomicfu
        transformJsTestAtomicfu
    }

    preparePublishNpm.outputs.upToDateWhen { false }

    task publishNpm(type: NpmTask, dependsOn: [preparePublishNpm]) {
       // project.logger.lifecycle('npmDeployDir= '+npmDeployDir)
       // project.logger.lifecycle('npmDeployTag= '+npmDeployTag)
       // project.logger.lifecycle('authToken= '+authToken)
        workingDir = npmDeployDir
        def deployArgs = ['publish',
                          "--//registry.npmjs.org/:_authToken=$authToken",
                          "--tag=$npmDeployTag"]
        doFirst {
            if (dryRun == "true") {
                println("$npmDeployDir \$ npm arguments: $deployArgs")
                args = ['pack']
            } else {
                args = deployArgs
            }
        }
    }
}
